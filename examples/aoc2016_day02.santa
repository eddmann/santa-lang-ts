input: read("aoc://2016/2")

let directions = #{"U": [-1, 0], "D": [1, 0], "L": [0, -1], "R": [0, 1]};

let find_key = |value, dictionary| fold(nil, |_, v, k| if v == value { break k }, dictionary);

let generate_keypad = |input| {
  zip(0.., lines(input))
    |> flat_map(|[y, row]| {
      zip(0.., row)
        |> filter(|[_, char]| char != ".")
        |> map(|[x, char]| [[y, x], char]);
    })
    |> dict;
}

let calc_keycode = |keypad, initial_key, instructions| {
  let mut pos = find_key(initial_key, keypad);
  let mut code = "";

  instructions |> each(|instruction| {
    instruction |> each(|direction| {
      let next_pos = pos `vec_add` directions[direction];
      if keypad[next_pos] {
        pos = next_pos;
      }
    });
    code = code + keypad[pos];
  });

  code;
};

part_one: {
  let keypad = generate_keypad("123\n456\n789");
  calc_keycode(keypad, "5", lines(input));
}

part_two: {
  let keypad = generate_keypad("..1..\n.234.\n56789\n.ABC.\n..D..");
  calc_keycode(keypad, "5", lines(input));
}

test: {
  input: "ULL\nRRDDD\nLURDL\nUUUUD"
  part_one: "1985"
  part_two: "5DB3"
}
